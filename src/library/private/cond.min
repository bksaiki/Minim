;;;   
;;; 'cond'
;;;

(import "and-or.min" "stx.min")
(export cond else)

(define-syntaxes (cond)
  (lambda (stx)
    (if (not (stx-list? stx))
        (raise-syntax-error 'cond "bad-syntax" stx)
        (let-values ([(e) (stx-cdr stx)]
                     [(serr) (lambda (msg at) (raise-syntax-error 'cond msg stx at))])
          (if (stx-null? e)
              (quote-syntax (void))
              (let-values ([(c) (stx-car e)])
                (if (and (stx-list? c) (not (null? c)))
                    (let-values ([(test) (stx-car c)]
                                 [(then) (stx-cdr c)]
                                 [(then-clause)
                                  (lambda (then)
                                    (let-values ([(then1) (stx-cdr then)])
                                      (if (stx-null? then1)
                                          (stx-car then)
                                          (cons (quote-syntax let)
                                            (cons (quote-syntax ())
                                            then)))))]
                                 [(else?)
                                  (lambda (i)
                                    (and (identifier? i)
                                         (free-identifier=? i (quote-syntax else))))])
                      (if (null? then)
                          (serr "missing body" c)
                          (if (else? test)
                              (if (null? (stx-cdr e))
                                  (datum->syntax (then-clause then))
                                  (serr "'else' clause must be last" c))
                              (datum->syntax
                                (list
                                  (quote-syntax if)
                                  test
                                  (then-clause then)
                                  (cons (quote-syntax cond) (stx-cdr e)))))))
                    (serr "missing clause" c))))))))

(define-syntaxes (else)
  (lambda (stx)
    (raise-syntax-error 'else "not allowed as an expression" stx)))
