;;;
;;; Compiler registers
;;;

(import "../base.min")
(export define-registers
        make-register-set
        validate-register-set!
        make-register-allocator
        register-allocator-new)

(define required-registers '(%cc %sp %env %ret))

;;
;;  Machine-dependent register set
;;

(define-record-type register-set
  (fields real aliases)
  (opaque #t))

(define (validate-register-set! rset)
  (define aliases (register-set-aliases rset))
  (for-each (lambda (r)
              (unless (assoc r aliases)
                (error 'validate-register-set! "missing required register" r)))
            required-registers))

(define-syntax (define-registers stx)
  (syntax-case stx (reserved allocable)
    [(_ id (reserved [rreg-alias ... rreg rreg-reserved?] ...)
           (allocable [areg-alias ... areg areg-reserved?] ...))
     (begin
       (unless (identifier? #'id)
         (syntax-error 'define-enumeration "bad syntax" stx #'id))
       #'(define id
           (let ([rregs '(rreg ...)]
                 [rregs-aliases '((rreg-alias ...) ...)]
                 [rregs-reserved? '(rreg-reserved? ...)]
                 [aregs '(areg ...)]
                 [aregs-aliases '((areg-alias ...) ...)]
                 [aregs-reserved? '(areg-reserved? ...)])
             (let* ([registers (append (map list rregs rregs-aliases rregs-reserved?)
                                       (map list aregs aregs-aliases aregs-reserved?))]
                    [aliases (let loop ([registers registers] [alias-map '()])
                               (cond
                                 [(null? registers) alias-map]
                                 [else
                                  (define reg (caar registers))
                                  (define aliases (cadar registers))
                                  (loop (cdr registers)
                                              (append (map (lambda (a) (cons a reg)) aliases)
                                                      alias-map))]))]
                    [rset (make-register-set registers aliases)])
               (validate-register-set! rset)
               rset))))]
    [(_ . _)
     (syntax-error 'define-registers "bad syntax" stx)]))

;;
;;  Register allocator
;;

(define-record-type register-allocator
  (fields target-set virtual-set)
  (opaque #t)
  (protocol
    (lambda (p)
      (lambda (rset)
        (p rset '())))))

(define $make-register-allocator
  (record-constructor register-allocator))

(define (register-allocator-has-register? allocator r)
  (define rset (register-allocator-target-set allocator))
  (assoc r (register-set-real rset)))

(define (register-allocator-new allocator)
  (define vset (register-allocator-virtual-set allocator))
  (define name (string->symbol (string-append "%t" (string->number (length vset)))))
  (define allocator* ($make-register-allocator rset (cons name vset)))
  (values name allocator*))
