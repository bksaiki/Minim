;;;
;;;  Compiler entry point
;;;   

(import "base.min" "module.min")

; syntax
(define (stx-pair? stx)
  (if (pair? stx)
      #t
      (if (syntax? stx)
          (pair? (syntax-e stx))
          #f)))

(define (stx-null? stx)
  (if (null? stx)
      #t
      (if (syntax? stx)
          (null? (syntax-e stx))
          #f)))

(define (stx-car stx)
  (if (syntax? stx)
      (car (syntax-e stx))
      (car stx)))

(define (stx-cdr stx)
  (if (syntax? stx)
      (cdr (syntax-e stx))
      (cdr stx)))

(define (identifier? stx)
  (and (syntax? stx) (symbol? (syntax-e stx))))

(define ((_-form? form-name) expr)
    (and (stx-pair? expr)
         (let ([head (stx-car expr)])
           (and (identifier? head)
                (eq? (syntax-e head) form-name)))))

(define define-values-form? (_-form? 'define-values))
(define define-syntaxes-form? (_-form? 'define-syntaxes))
(define let-values-form? (_-form? 'let-values))
(define letrec-values-form? (_-form? 'letrec-values))
(define let-syntaxes-form? (_-form? 'let-syntaxes))
(define quote-form? (_-form? 'quote))
(define quote-syntax-form? (_-form? 'quote-syntax))
(define lambda-form? (_-form? 'lambda))
(define begin-form? (_-form? 'begin))
(define if-form? (_-form? 'if))

; syntax would parse into a self-evaluating expression
(define (stx-self-evaluating? stx)
  (define datum (syntax-e stx))
  (or (boolean? datum)
      (number? datum)
      (string? datum)
      (vector? datum)
      (char? datum)))

; registers
(define cc-reg     'RCC)
(define env-reg    'RENV)
(define i-reg      'RIP)
(define sp-reg     'RSP)
(define res-reg    'RRET)
(define tmp0-reg   'R0)
(define tmp1-reg   'R1)

(define (make-compiled-closure formals instrs)
  (cond
    [(null? formals)
     (list 'fun
       (list 'args '())
       (list 'instrs instrs))]
    [else
     (error 'make-compiled-closure "unimplemented" formals instrs)]))

(define (compile/expr expr procedure! literal!)
  (let rec ([expr expr])
    (cond
      [(lambda-form? expr)
       (define formals (stx-car (stx-cdr expr)))
       (define body (stx-cdr (stx-cdr expr)))
       (define instrs
         (let loop ([body body])
           (cond
             [(null? body) '()]
             [else (append (rec (car body)) (loop (cdr body)))])))
       (define pcode (make-compiled-closure (syntax->datum formals) instrs))
       (define idx (procedure! pcode))
       (list (list 'closure idx))]
      [(stx-list? expr)
       (define op (stx-car expr))
       (define args (stx-cdr expr))
       (let loop ([args args])
         (cond
           [(null? args) '()]
           [else
            (append
              (rec (car args))
              (list (list 'push))
              (loop (cdr args)))]))]
      [(stx-self-evaluating? expr)
       (define idx (literal! (syntax-e expr)))
       (list (list 'literal idx))]
      [else
       (error 'compile/expr "unimplemented" expr)])))

(define (compile/top-level expr top-level! procedure! literal!)
  (display " <top-level: ") (write expr) (display ">") (newline)
  (cond
    [(define-values-form? expr)
     ; decompose
     (define names (stx-car (stx-cdr expr)))
     (define e (stx-car (stx-cdr (stx-cdr expr))))
     ; process identifiers
     (define ids (map syntax-e (syntax-e names)))
     (define idxs (map top-level! ids))
     ; process expression
     (compile/expr e procedure! literal!)
     (cons
       (list 'bind-check (length ids))
       (map (lambda (i) (list 'bind i)) ids))]
    [else (compile/expr expr)]))

(define (compile/file path)
  (define top-levels '())
  (define procedures '())
  (define literals '())

  (define (register-top-level! name)
    (define count (length top-levels))
    (set! top-levels (cons name top-levels))
    count)

  (define (register-procedure! pcode)
    (define count (length procedures))
    (set! procedures (cons pcode procedures))
    count)

  (define (register-literals! lit)
    (define count (length literals))
    (set! literals (cons lit literals))
    count)

  (define-values (exprs xforms imports exports _) (read/module path))
  (define module-body
    (let loop ([exprs exprs])
      (cond
        [(null? exprs) '()]
        [else
         (append
           (compile/top-level (car exprs)
                              register-top-level!
                              register-procedure!
                              register-literals!)
           (loop (cdr exprs)))])))
  
  ; lists are in reverse order
  (set! top-levels (reverse top-levels))
  (set! procedures (reverse procedures))
  (set! literals (reverse literals))

  ; debug print report
  (display "<report") (newline)
  (display " top-levels: ") (write top-levels) (newline)
  (display " procedures: ") (write procedures) (newline)
  (display " literals  : ") (write literals) (newline)
  (display " body      : ") (write module-body) (newline)
  (display ">") (newline)

  (void))

; entry point
(define (main args)
  ; currently no flag handling
  (define fnames (cdr args))
  (let loop ([fnames fnames])
    (unless (null? fnames)
      (define name (car fnames))
      (define path
        (if (is-absolute-path? name)
            name
            (build-path (current-directory) name)))
      (display "<compiling ") (write name) (display ">") (newline)
      (compile/file path)
      (loop (cdr fnames)))))

(main (command-line))
