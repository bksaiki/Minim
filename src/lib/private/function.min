;
;   Functional programming constructs
;

(%import "pre-base.min")

(def identity (x) x)

(def negate (f)
  (lambda args (not (apply f args))))

(def const (x) (lambda args x))

(def curry (f . args)
  (unless (procedure? f)
    (error 'curry "expected a procedure"))
  (def arity (procedure-arity f))
  (def min-arity (if (pair? arity) (car arity) arity))
  (lambda args2
    (let ([args* (append args args2)])
      (if (< (length args*) min-arity)
          (apply curry f args*)
          (apply f args*)))))

(def curryr (f . args)
  (unless (procedure? f)
    (error 'curryr "expected a procedure"))
  (def arity (procedure-arity f))
  (def min-arity (if (pair? arity) (car arity) arity))
  (lambda args2
    (let ([args* (append args2 args)])
      (if (< (length args*) min-arity)
          (apply curryr f args*)
          (apply f args*)))))

(%export identity const
         negate curry curryr)
