;;;
;;; Tests for 'syntax-case'
;;;

(import "../lib/base.min")

(define num-failed 0)

(define (check-equal? d0 d1)
  (unless (equal? d0 d1)
    (display "[FAIL] expected ")
    (write d1)
    (display ", received ")
    (write d0)
    (newline)
    (set! num-failed (+ num-failed 1))))

;; easy

(define-syntax (macro-1 stx)
  (syntax-case stx ()
    [(_ a) #'1]))

(define-syntax (macro-2 stx)
  (syntax-case stx ()
    [(_ a) #'a]))

(define-syntax (macro-3 stx)
  (syntax-case stx ()
    [(_ a b) #'(list a b)]))

(define-syntax (macro-4 stx)
  (syntax-case stx ()
    [(_ a) #'(list a)]
    [(_ a b) #'(list a b)]))

(define-syntax (macro-5 stx)
  (syntax-case stx ()
    [(_ a . b) #'(cons a b)]))

(define-syntax (macro-6 stx)
  (syntax-case stx (foo)
    [(_ foo) #'(quote good)]
    [_ #'(quote bad)]))

(define-syntax (macro-7 stx)
  (syntax-case stx ()
    [(_ x) (identifier? #'x)  #'(quote good)]
    [_ #'(quote bad)]))

(define-syntax (macro-8 stx)
  (syntax-case stx ()
    [(_ a ... b) #''(b (a ...))]
    [_ #''bad]))

(define-syntax (macro-9 stx)
  (syntax-case stx ()
    [(_ a ... . b) #''(b (a ...))]
    [_ #''bad]))

(define-syntax (macro-10 stx)
  (syntax-case stx ()
    [(_ a ... b c . d) #''(b c (a ...) d)]
    [_ #''bad]))

(define-syntax (macro-11 stx)
  (syntax-case stx ()
    [(_ a b c d e ...) #'(+ a b c)]
    [(_ a b) #'(+ a b c)]
    [_ #''bad]))

(define-syntax (macro-12 stx)
  (syntax-case stx ()
    [(_ #()) #''empty-vec]
    [(_ #(a)) #''vec-len-1]
    [(_ #(a b c)) #''vec-len-3]))

(define-syntax (macro-13 stx)
  (syntax-case stx ()
    [(_ #(a ...)) #''(a ...)]))

(define-syntax (macro-14 stx)
  (syntax-case stx ()
    [(_ #(a ... b)) #''(b a ...)]))

(define-syntax (macro-15 stx)
  (syntax-case stx ()
    [(_ #(a b c ... d e)) #''((a b d e) c ...)]))

(define-syntax (macro-16 stx)
  (syntax-case stx ()
    [(_ #(a (b ...) c)) #''(a c b ...)]))

(check-equal? (macro-1 2) 1)
(check-equal? (macro-1 2) 1)
(check-equal? (macro-2 1) 1)
(check-equal? (macro-3 1 2) '(1 2))

(check-equal? (macro-4 1) '(1))
(check-equal? (macro-4 1 2) '(1 2))

(check-equal? (macro-5 1 . 2) '(1 . 2))

(check-equal? (macro-6 foo) 'good)
(check-equal? (macro-6 +) 'bad)

(check-equal? (macro-7 foo) 'good)
(check-equal? (macro-7 (+ 1 2)) 'bad)

(check-equal? (macro-8) 'bad)
(check-equal? (macro-8 1) '(1 ()))
(check-equal? (macro-8 1 2) '(2 (1)))
(check-equal? (macro-8 1 2 3) '(3 (1 2)))

(check-equal? (macro-9) '(() ()))
(check-equal? (macro-9 . 1) '(1 ()))
(check-equal? (macro-9 1) '(() (1)))
(check-equal? (macro-9 1 2) '(() (1 2)))
(check-equal? (macro-9 1 2 . 3) '(3 (1 2)))
(check-equal? (macro-9 1 2 . (3 4)) '(() (1 2 3 4)))

(check-equal? (macro-10 1) 'bad)
(check-equal? (macro-10 1 2) '(1 2 () ()))
(check-equal? (macro-10 1 2 3) '(2 3 (1) ()))
(check-equal? (macro-10 1 2 3 4) '(3 4 (1 2) ()))
(check-equal? (macro-10 1 2 3 4 . 5) '(3 4 (1 2) 5))

(check-equal? (macro-11) 'bad)
(check-equal? (macro-11 1) 'bad)
(check-equal? (macro-11 1 2 3) 'bad)

(check-equal? (macro-12 #()) 'empty-vec)
(check-equal? (macro-12 #(1)) 'vec-len-1)
(check-equal? (macro-12 #(1 2 3)) 'vec-len-3)

(check-equal? (macro-13 #()) '())
(check-equal? (macro-13 #(1)) '(1))
(check-equal? (macro-13 #(1 2 3)) '(1 2 3))

(check-equal? (macro-14 #(1)) '(1))
(check-equal? (macro-14 #(1 2)) '(2 1))
(check-equal? (macro-14 #(1 2 3)) '(3 1 2))

(check-equal? (macro-15 #(1 2 3 4)) '((1 2 3 4)))
(check-equal? (macro-15 #(1 2 3 4 5)) '((1 2 4 5) 3))
(check-equal? (macro-15 #(1 2 3 4 5 6)) '((1 2 5 6) 3 4))

(check-equal? (macro-16 #(1 () 2)) '(1 2))
(check-equal? (macro-16 #(1 (2) 3)) '(1 3 2))
(check-equal? (macro-16 #(1 (2 3) 4)) '(1 4 2 3))

;; actual Scheme macros

(define-syntax (mylet stx)
  (syntax-case stx ()
    [(_ ([var val] ...) body ...)
     #'(let-values ([(var) val] ...) body ...)]))

(define-syntax mywhen
  (syntax-rules ()
    ((_ test e1 e2 ...) (if test (begin e1 e2 ...) (void)))))

(define-syntax myunless
  (syntax-rules ()
    ((_ test e1 e2 ...) (mywhen (not test) (begin e1 e2 ...)))))

; (define-syntax mydo
;   (lambda (orig-x)
;     (syntax-case orig-x ()
;       ((_ ((var init . step) ...) (e0 e1 ...) c ...)
;        (with-syntax (((step ...)
;                       (map (lambda (v s)
;                              (syntax-case s ()
;                                (() v)
;                                ((e) (syntax e))
;                                (_ (syntax-error orig-x))))
;                          (syntax (var ...))
;                          (syntax (step ...)))))
;          (syntax-case (syntax (e1 ...)) ()
;            (()
;             (syntax (let loop ((var init) ...)
;                        (if (not e0)
;                            (void)
;                            (begin c ... (loop step ...))))))
;            ((e1 e2 ...)
;             (syntax (let loop ((var init) ...)
;                        (if e0
;                            (begin e1 e2 ...)
;                            (begin c ... (loop step ...))))))))))))

(check-equal? (mylet () 1) 1)
(check-equal? (mylet ([x 1]) x) 1)
(check-equal? (mylet ([x 1] [y 2]) y) 2)
(check-equal? (mylet ([x 1] [y 2]) (set! x 3) (+ x y)) 5)

(check-equal? (mywhen #t 1) 1)
(check-equal? (mywhen #t 1 2 3) 3)
(check-equal? (mywhen #f 1 2 3) (void))

(check-equal? (myunless #f 1) 1)
(check-equal? (myunless #f 1 2 3) 3)
(check-equal? (myunless #t 1 2 3) (void))

; (check-equal?
;   (mydo ((i 0 (+ i 1)))
;     ((= i 5) i)
;     i)
;   5)

(when (> num-failed 0)
  (error #f "test failed"))
