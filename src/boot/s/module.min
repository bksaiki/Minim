;;;
;;;   Minim boot library for the bootstrap interpreter
;;;   Brett Saiki
;;;

;;;
;;;   File reader for importing Minim files
;;;

;; Base environment when loading modules
(define $base-env (environment))

;; Module cache
;;  name -> (xforms, exports)
(define $module-table '())
;;  name -> env
(define $module-envs '())

;; Loads a module with path `name`
(define ($load-module name)
  ;; current parameters
  (define cwd (current-directory))

  ;; resolve path
  (define path
    (if (is-absolute-path? name)
        name
        (build-path cwd name)))
  (define new-cwd (car (split-path path)))

  ;; module information
  (define exported '())
  (define imported '())
  (define imported-xforms '())
  (define module-env (extend-environment $base-env))
  (define module-stx-env (extend-environment $base-env))
  (define module-xforms '())

  ;; read phase
  (define ($read)
    (define p (open-input-file path))
    (let loop ([exprs '()])
      (define datum (read p))
      (cond
        [(eof-object? datum)
         (close-input-port p)
         (reverse exprs)]
        [else
         (loop (cons datum exprs))])))

  ;; import phase
  (define ($import exprs)
    (let loop ([exprs exprs] [evalable '()])
      (cond
        [(null? exprs)
         (reverse evalable)]
        [else
         (define expr (syntax-e (car exprs)))
         (cond
           [(and (pair? expr) (eq? (syntax-e (car expr)) 'import))
            (let loop2 ([imports (cdr expr)])
              (cond
                [(null? imports)
                 (loop (cdr exprs) evalable)]
                [(pair? imports)
                 (define maybe-import (syntax-e (car imports)))
                 (cond
                   [(string? maybe-import)
                    (define path (build-path new-cwd maybe-import))
                    (define maybe-cached (assoc path $module-table))
                    (define from-module
                      (if maybe-cached
                          maybe-cached
                          (begin
                            ($load-module path)
                            (assoc path $module-table))))
                    (set! imported-xforms (append (car from-module) imported-xforms))
                    (set! imported (append (cdr from-module) imported))
                    (loop2 (cdr imports))]
                   [else
                    (error 'import "bad syntax")])]
                [else
                 (error 'import "bad syntax")]))]
           [(and (pair? expr) (eq? (syntax-e (car expr)) 'export))
            (let loop2 ([exports (cdr expr)])
              (cond
                [(null? exports)
                 (loop (cdr exprs) evalable)]
                [(pair? exports)
                 (define maybe-export (syntax-e (car exports)))
                 (cond
                   [(symbol? maybe-export)
                    (set! exported (cons maybe-export exported))
                    (loop2 (cdr exports))]
                   [else
                    (error 'export "bad syntax")])]
                [else
                 (error 'export "bad syntax")]))]
           [else
            (loop (cdr exprs) (cons (car exprs) evalable))])])))

  ;; expand phase
  (define ($expand exprs)
    ;; setup environment
    (let loop ([imported imported])
      (cond
        [(null? imported) (void)]
        [else
         (define name (caar imported))
         (define value (cdar imported))
         (environment-set-variable-value! module-stx-env name value)
         (loop (cdr imported))]))

    ;; apply transformers
    (define old-xforms $xforms)
    (set! $xforms imported-xforms)
    (let loop ([exprs exprs] [acc '()])
      (cond
        [(null? exprs)
         (set! $xforms old-xforms)
         (reverse acc)]
        [else
         (define expanded (expand (car exprs)))
         (define expanded* (syntax-e expanded))
         (cond
           [(and (pair? expanded*) (eq? (syntax-e (car expanded*)) 'define-syntax))
            (define (panic!) (error "bad define-syntax form"))
            (define rib (cdr expanded*))
            (cond
              [(and (pair? rib) (symbol? (syntax-e (car rib))))
               (define xform-name (syntax-e (car rib)))
               (define rib (cdr rib))
               (cond
                 [(and (pair? rib) (null? (cdr rib)))
                  (define xform (eval (syntax->datum (car rib)) module-stx-env))
                  (set! module-xforms (cons (cons xform-name xform) module-xforms))]
                 [else
                  (panic!)])]
               [else
                (panic!)])
            (loop (cdr exprs) acc)]
           [else
            (loop (cdr exprs) (cons expanded acc))])])))

  ;; eval phase
  (define ($eval exprs)
    ;; set up environment
    (let loop ([imported imported])
      (cond
        [(null? imported) (void)]
        [else
         (define name (caar imported))
         (define value (cdar imported))
         (environment-set-variable-value! module-env name value)
         (loop (cdr imported))]))

    ;; evaluate
    (let loop ([exprs exprs] [result (void)])
      (cond
        [(null? exprs) result]
        [else (loop (cdr exprs) (eval (syntax->datum (car exprs)) module-env))])))

  ;; apply non-eval phases
  (define raw-exprs ($read))
  (define non-import-exprs ($import raw-exprs))
  (define expanded-exprs ($expand non-import-exprs))

  ;; evaluate
  (current-directory new-cwd)
  (define last ($eval expanded-exprs))
  (current-directory cwd)

  ;; handle exports
  (define export-values '())
  (define export-xforms '())
  (let loop ([exported exported])
    (cond
      [(null? exported) (void)]
      [else
       (define export (car exported))
       (define maybe-xform (assoc export module-xforms))
       (cond
         [maybe-xform
          (set! export-xforms (cons (cons export maybe-xform) export-xforms))]
         [else
          (define value
            (environment-variable-value
              module-env
              export
              (lambda () (error 'export "undefined export" export))))
          (set! export-values (cons (cons export value) export-values))])
       (loop (cdr exported))]))

  ;; update module tables
  (define entry (cons path (cons export-xforms export-values)))
  (set! $module-table (cons entry $module-table))
  (set! $module-envs (cons (cons path module-env) $module-envs))

  ;; and done
  last)

(define (load name)
  ($load-module name))

(define (enter! name)
  ;; resolve path
  (define path
    (if (is-absolute-path? name)
        name
        (build-path (current-directory) name)))
  (define maybe-env (assoc path $module-envs))
  (cond [(not maybe-env)
         (error 'enter! "unknown module" name)])
  ;; set up environment
  (set! $xforms (car (assoc path $module-table)))
  (define env (extend-environment maybe-env))

  ;; REPL
  (let loop ()
    ;; read
    (display "> ")
    (define stx (read))
    (cond [(eof-object? stx) (exit)])
    ;; expand
    (define stx* (expand stx))
    ;; evaluate
    (define result (eval (syntax->datum stx*) env))
    ;; display
    (cond [(not (void? result)) (writeln result)])
    (loop)))
